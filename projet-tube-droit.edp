//--------Maillage---------l

// paramètres de la géométrie

real largeur = 1e-6;         // diamètre / largeur
real longueur = 5*largeur;  // longueur du tube
real Lc = 1*largeur;         // longueur partie chauffe

// paramètres du maillage

real h = largeur * 1e-1;

// taille des éléments

int Nx = longueur / h + 1;
int Ny = largeur / h + 1;

// labels des bords

int labelNeutre = 0;
int labelEntree1 = 1;
int labelEntree2 = 2;
int labelSortie = 3;

// le point A est en bas à gauche, il a pour coordonnées A(xA, yA)
// le point B est en haut à droite, il a pour coordonnées B(xB, yB)

real xA = 0;
real yA = 0;
real xB = xA + longueur;
real yB = yA + largeur;

//Omega F (partie gauche)
border bordGauche1(t = yB, yB/2) {x = xA; y = t; label = labelEntree1;};
border bordGauche2(t = yB/2, yA) {x = xA; y = t; label = labelEntree2;};
border bordHaut(t = xB, xA) {x = t; y = yB; label = labelNeutre;};
border bordBas(t = xA, xB) {x = t; y = yA; label = labelNeutre;}; 
border bordDroit(t = yA, yB) {x = xB; y = t; label = labelSortie;};

mesh Th = buildmesh(
                    bordGauche1(Ny) + bordGauche2(Ny) + bordDroit(Ny)
                    + bordBas(Nx) + bordHaut(Nx)
                    );

// affichage du maillage

plot(Th, wait = 1, cmm = "maillage Th");

//-------------Paramètres------------

// paramètres de l'équation aux dérivées partielles
// liés à la géométrie
real xbar = largeur;    // diamètre canaux
real ubar = 1e-3;       // vitesse de l’écoulement

// liés au fluide
real rho = 1e3;         // masse volumique
real eta = 1.753e-3;    // viscosité dynamique (eau a 0 degrés)
real eta20 = 1e-3;      // viscosité dynamique (eau a 20 degrés)
real rayonParticule = 1e-9; // taille des molécules d'eau
real gamma = 7e-22 / rayonParticule;         // diffusion

// liés à l'expérience
real tbar = sqrt(rho * xbar^3 / (eta20 * ubar));       // temps caractéristique
//real tbar = 1e-3;
real pbar = 8 * ubar * eta20 * longueur / (xbar^2);
real cbar = 200;        // concentration caractéristique
real tempFroide = 20;   // en degrés
real tempChaude = 90;
real lbar = longueur;

//-----------Conditions aux bords-------------

real pressionAtm = 0;
real pressionEntree = pressionAtm + pbar;
real pressionSortie = pressionAtm;

real dP = pressionEntree - pressionSortie;
func uxini = - dP/(2*eta20*longueur)*(y-largeur)*(y);  // vitesse entrée
func uyini = 0;

// paramètres de la simulation

int typeSchema = 1;         // Euler implicite
real stabilisation = 1e-10; // coefficient de stabilisation
real T = longueur / ubar;   // temps final de simulation
real tau = T / 20;          // pas de simulation
int M = T / tau;            // nombre d'instants de simulation

// fonction d'adaptation de la viscosité dynamique

func real alpha(real theta){
    real a, b, c, d, e, f;
    a = -2.53958848e-16;
    b = -2.76506905e-12;
    c = 5.95400333e-10;
    d = -4.52299564e-08;
    e = 1.72329803e-06;
    return (a * theta^4 + b * theta^3 + c * theta^2 + d * theta + e)/(1.753*1e-6);
}

//-------Définition des espaces éléments finis et du problème-------

fespace Ph1(Th, P1); // pour la pression, température et viscosité
fespace Ph2(Th, P2); // pour la concentration
fespace Vh2(Th, [P2, P2]); // pour la vitesse

// fonction pour la viscosité

real hChauffe = 5*h;

Ph1 alphaOmega = 
    ((longueur - Lc)/2 < x & x < (longueur + Lc)/2)
        * alpha(tempChaude)
    + (x < (longueur - Lc)/2 - hChauffe || (longueur + Lc)/2 + hChauffe < x )
        * alpha(tempFroide)
    + ((longueur - Lc)/2 - hChauffe < x & x < (longueur - Lc)/2)
        * (alpha(tempFroide) + (alpha(tempChaude) - alpha(tempFroide))
        * (x - ((longueur - Lc)/2 - hChauffe)) / hChauffe)
    + ((longueur + Lc)/2 < x & x < (longueur + Lc)/2 + hChauffe)
        * (alpha(tempChaude) + (alpha(tempFroide) - alpha(tempChaude))
        * (x - (longueur + Lc)/2) / hChauffe)
        ;

real tempFroideK = tempFroide;// + 273;
real tempChaudeK = tempChaude + 273;// + 10 + 4e2;

Ph1 thetaOmega = 
    ((longueur - Lc)/2 < x & x < (longueur + Lc)/2)
        * (tempChaudeK)
    + (x < (longueur - Lc)/2 - hChauffe || (longueur + Lc)/2 + hChauffe < x )
        * (tempFroideK)
    + ((longueur - Lc)/2 - hChauffe < x & x < (longueur - Lc)/2)
        * (tempFroideK + (tempChaudeK - tempFroideK)
        * (x - ((longueur - Lc)/2 - hChauffe)) / hChauffe)
    + ((longueur + Lc)/2 < x & x < (longueur + Lc)/2 + hChauffe)
        * (tempChaudeK + (tempFroideK - tempChaudeK)
        * (x - (longueur + Lc)/2) / hChauffe)
        ;

plot(thetaOmega, value = 1, fill = 1, wait = 1);

// macros

macro grad(u) [dx(u), dy(u)]                //
macro Grad(U) [grad(U#x), grad(U#y)]        //
macro divergence(ux, uy) (dx(ux) + dy(uy))  //

// fonctions éléments finis

Vh2 [ux, uy];   // vitesse recherchée
Vh2 [uux, uuy]; // vitesses à l'instant précédent
Ph1 p;          // pression recherchée
Vh2 [uxinih, uyinih] = [ uxini, uyini ]; // vitesses initiales interpolées

Vh2 [vx, vy];   // fonction test vitesse
Ph1 q;          // fonction test pression

Ph2 c, v;       // concentration et fonction test
Ph2 cc;         // concentration à l'instant précédent
// pas besoin d'initialiser c, on le cherche

cc = 0;

// problème de Stokes

problem Stokes([ ux, uy, p ], [ vx, vy, q ]) =

    // terme inertiel
    
    int2d(Th)(
                (rho / tau ) * [ux, uy]' * [vx, vy]
    )
  + int2d(Th)(
                - (rho / tau ) * [uux, uuy]' * [vx, vy]
    )
    
    // termes de viscosité, incompressibilité, pression
    
  - int2d(Th)(
                typeSchema * (
                - alphaOmega * eta * 
                (grad(ux) ' * grad(vx) + grad(uy)' * grad(vy))
                + divergence(ux, uy) * q
                + divergence(vx, vy) * p
                //+ stabilisation * p * q
                )
    )
    
    // termes de bord en flux sortant

  - int1d(Th, labelEntree1, labelEntree2, labelSortie)(
                            typeSchema * alphaOmega * eta * N.x *
                            (dx(ux)*vx + dx(uy)*vx )
    )
    

    // terme de bord en pression

  - int1d(Th, labelEntree1, labelEntree2, labelSortie)(
                            - p * vx * N.x
    )
    
    // termes qui dépendent de u pour l'autre schéma :
    
  - int2d(Th)(
                (1 - typeSchema) * (
                - alphaOmega * eta * 
                (grad(uux) ' * grad(vx) + grad(uuy)' * grad(vy))
                + divergence(uux, uuy) * q
                //+ stabilisation * p * q
                )
    )
  - int2d(Th)(
                (1 - typeSchema) * divergence(vx, vy) * p
  )
    
    // termes de bord en flux sortant

  - int1d(Th, labelEntree1, labelEntree2, labelSortie)(
                            (1 - typeSchema) * alphaOmega * eta * N.x *
                            (dx(uux) * vx + dx(uuy) * vx)
    )
    

    //+ on(labelEntree, ux = uxinih, uy = uyinih)  // entree profil Poiseuille
    + on(labelEntree1, labelEntree2, uy = uyinih)  // entree vitesse unidimensionnelle
    + on(labelEntree1, labelEntree2, p = pressionEntree)
    + on(labelSortie, p = pressionSortie)
    + on(labelNeutre, ux = 0, uy = 0)    // bords solides
    ;

// problème de convection-diffusion

problem ConvectionDiffusion(c, v) = 
      int2d(Th)(c * v / tau)
      
    - int2d(Th)(cc * v / tau)
    
    + int2d(Th)(
                typeSchema * (thetaOmega * gamma * grad(c) ' * grad(v)
                + grad(c) ' * [ux, uy] * v)
    )

    //- int1d(Th, labelNeutre)(thetaOmega * gamma * N.x * dx(cc) * v )
    /*
    - int1d(Th, labelEntree)(
                typeSchema * thetaOmega * gamma * grad(c)' * [N.x, N.y] * v
    )
    */
    + int2d(Th)(
                (1 - typeSchema) * (thetaOmega * gamma * grad(cc) ' * grad(v)
                + grad(cc) ' * [ux, uy] * v)
    )
    //- int1d(Th, labelNeutre)(thetaOmega * gamma * N.x * dx(cc) * v )
    /*
    - int1d(Th, labelEntree1, labelEntree2)(
                (1 - typeSchema) * thetaOmega * gamma 
                * grad(cc)' * [N.x, N.y] * v 
    )
    */
    + on(labelEntree1, c = cbar)
    ;


// résolution du problème pas à pas

for (int i = 1; i <= M; i++){    
    // résolution des problèmes
    Stokes;
    ConvectionDiffusion;
    
    // mise à jour des conditions
    cc = c;
    [uux, uuy] = [ux, uy];

    // affichage
    plot(c, value = 1, fill = 1, wait = 1, cmm = "concentration P2");
    //plot( [ ux, uy ], wait = 1, value = 1, fill = 1, cmm = "champ de vitesse P2");
    cout << "t =" << tau*i << endl;
}
























