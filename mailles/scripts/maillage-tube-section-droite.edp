// paramètres de la géométrie

real largeur = 1e-6;        // diamètre / largeur
real longueur = 5*largeur;  // longueur du tube
real Lc = 1*largeur;        // longueur partie chauffe

// paramètres du maillage

real h = largeur * 1e-1;

// taille des éléments

int Nxfroid = (longueur - Lc)/(2*h) + 1;
int Nyfroid = largeur / h + 1;
int Nxchaud = Lc / h + 1;
int Nychaud = Nyfroid;

// labels des bords

int labelNeutre = 0;
int labelEntree = 1;
int labelChauffex = 2;
int labelChauffey = 3;
int labelSortie = 4;

// construction des bords

border bentree(t = 0, 1) {
  x = 0;
  y = largeur*(1-t);
  label = labelEntree; // entrée
}

border bf1(t = 0, 1) {
  x = (longueur - Lc)*t/2;
  y = 0;
  label = labelNeutre; // neutre
}

border bf2(t = 0, 1) {
  x = (longueur - Lc)/2 - t*(longueur - Lc)/2;
  y = largeur;
  label = labelNeutre;
}

border bf3(t = 0, 1) {
  x = (longueur + Lc)/2 + t*(longueur - Lc)/2;
  y = 0;
  label = labelNeutre;
}

border bf4(t = 0, 1) {
  x = longueur - t*(longueur - Lc)/2;
  y = largeur;
  label = labelNeutre;
}

border bc1(t = 0, 1) {
  x = (longueur - Lc)/2 + t*Lc;
  y = 0;
  label = labelChauffex; // chauffe
}

border bc2(t = 0, 1) {
  x = (longueur + Lc)/2;
  y = t*largeur;
  label = labelChauffey;
}

border bc3(t = 0, 1) {
  x = (longueur + Lc)/2 - t*Lc;
  y = largeur;
  label = labelChauffex;
}

border bc4(t = 0, 1) {
  x = (longueur - Lc)/2;
  y = largeur*(1-t);
  label = labelChauffey;
}

border bsortie(t = 0, 1) {
  x = longueur;
  y = largeur*t;
  label = labelSortie;  // sortie
}

// construction du maillage et affichage

mesh Th = buildmesh(bentree(Nyfroid*2) + bsortie(Nyfroid)
  + bf1(Nxfroid) + bf2(Nxfroid) + bf3(Nxfroid) + bf4(Nxfroid) 
  + bc2(Nychaud) + bc4(Nychaud)
  + bc1(Nxchaud) + bc3(Nxchaud));

plot(Th, wait = 1, cmm = "maillage Th");

savemesh(Th, "../tuyau-droit-gros-1700-elm.msh");

