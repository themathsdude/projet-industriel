//--------Maillage---------

// paramètres de la géométrie
real largeur = 1e-6;    // diamètre / largeur
real Long = 5*largeur;  // longueur du tube
real Lc = largeur;      // longueur partie chauffe

real l0 = 0;
real l = l0 + largeur;
real LF = (Long-Lc)/2; //longueur à gauche
real LP = LF; //longueur à droite
//longueurs cumulées (pour les coordonnées) maillage :
real L0 = 0, L1 = LF, L2 = L1 +  Lc, L = LP + LF + Lc; 

int n = 50;

//Omega F (partie gauche)
border OFu(t = L1, L0) {x = t; y = l; label = 1;}; //Label 1 = bords neutres
border OFl(t = l, l0) {x = L0; y = t; label = 2;}; // Label 2 = bord entrée
border OFd(t = L0, L1) {x = t; y = l0; label = 1;}; 
//Omega C (partie chauffe)
border OCu(t = L2, L1) {x = t; y = l; label = 3;}; 
border OCd(t = L1, L2) {x = t; y = l0; label = 3;}; // Label 3 = bords chauffe
//Omega P (partie droite)
border OPd(t = L2, L) {x = t; y = l0; label = 1;};
border OPr(t = l0, l) {x = L; y = t; label = 4;}; // Label 4 = bord sortie
border OPu(t = L, L2) {x = t; y = l; label = 1;};

mesh Th = buildmesh(OFd(n) + OFu(n) + OFl(n) 
    + OCd(n/2) + OCu(n/2) 
    + OPd(n) + OPr(n) + OPu(n));

// affichage maillage
plot(Th, wait = 1);

//mesh Th = readmesh("mailles/tuyau-droit-gros-1700-elm.msh");

//------------Paramètres-------------

// paramètres de l'équation aux dérivées partielles
// liés à la géométrie
real xbar = 1e-6;    // diamètre canaux
real ubar = 1e-3;    // vitesse de l’écoulement
real Lbar = 3e-1;

// liés au fluide
real rho = 1e3;          // masse volumique
real eta = 1.005e-6; 	 // viscosité dynamique (eau a 20 degrés)
real gamma = 1e-14;      // viscosité/diffusion

// liés à l'expérience
real tbar = 1e-3;       // temps caractéristique
real pbar = 1e4;        // pression caractéristique 
real cbar = 200;        // concentration caractéristique
real tempFroide = 20;
real tempChaude = 80;
real lbar = 2.5e-4;       // j'ai mis ça pour que l'écoulement de Poiseuille
// donne une pression d'entrée de l'ordre de pbar, histoire de comparer
// entre imposer pression entree ou imposer ecoulement poiseuille 

// conditions aux bords

real pressionEntree = pbar;
real pressionSortie = 0;

real dP = pressionEntree - pressionSortie;
func uxini = - dP/(2*eta*lbar)*(y-largeur)*(y);  // vitesse entrée
func uyini = 0;

// vitesse de l'ordre de pbar * xbar^2 / (8 * eta * L) 
// = 1e-8 / (8 * 1e-6 * L)
// = 1e4 / (8 * 5) = 1e3 / 4
// = ubar / 4 donc il faut un temps de l'ordre de xbar / ubar = 1e-3 pour
// observer l'écoulement sur ce tuyau

real stabilisation = 1e-10; // coefficient de stabilisation

// variables intermédiaires

real lambda = xbar * pbar / (rho * eta * ubar);
real T = tbar / 1e4;

func real alpha(real theta){
    real a, b, c, d, e, f;
    a = -2.53958848e-16;
    b = -2.76506905e-12;
    c = 5.95400333e-10;
    d = -4.52299564e-08;
    e = 1.72329803e-06;
    return (a * theta^4 + b * theta^3 + c * theta^2 + d * theta + e)/(1.753*1e-6);
}

// labels des bords

int labelNeutre = 1;
int labelEntree = 2;
int labelChauffe = 3;
int labelSortie = 4;

// définition des espaces éléments finis et du problème

fespace Ph0(Th, P0); // pour les indicatrices de région
fespace Ph1(Th, P1); // pour la pression
fespace Vh2(Th, [P2, P2]); // pour la vitesse

// fonction pour la viscosité
func falphaOmega =  2*largeur < x & x < 3*largeur ? 0.2 : 0.6;
Ph0 alphaOmega = falphaOmega;
plot(alphaOmega, value = 1, fill = 1, wait = 1);

// problèmes aux éléments finis

macro grad(u) [dx(u), dy(u)]                //
macro grad2(u) [grad(u#x), grad(u#y)]       //
macro divergence(ux, uy) (dx(ux) + dy(uy))  //

Vh2 [ux, uy]; // vitesse recherchée
Ph1 p;        // pression recherchée
Vh2 [uxinih, uyinih] = [ uxini, uyini ]; // vitesses initiales interpolées

Vh2 [vx, vy]; // fonction test vitesse
Ph1 q;        // fonction test pression

problem StokesStationnaire([ ux, uy, p ], [ vx, vy, q ]) =
    int2d(Th)(
              - (grad(ux) ' * grad(vx) + grad(uy)' * grad(vy)) * alphaOmega
              + divergence(ux, uy) * q
              + lambda * divergence(vx, vy) * p
              //+ stabilisation * p * q
              )

    + on(labelEntree, ux = uxinih, uy = uyinih)  // entree profil Poiseuille
    //+ on(labelEntree, ux = 1e-5, uy = uyinih)  // entree vitesse caractéristique
    //+ on(labelEntree, p = pressionEntree)
    //+ on(labelSortie, p = pressionSortie)
    + on(labelNeutre, labelChauffe, ux = 0, uy = 0)    // bords solides
    ;
    // soit on impose uxinih tel que le profil de poiseuille corresponde en pression d'entree à pbar ou corresponde en vitesse à ubar, c'est a dire bidouiller lbar
    // soit on impose p en entree et ux = 1e-5 = 0, sauf que on a une énorme perte de pression pour mettre le fluide en mouvement, donc est-ce + réaliste ?

StokesStationnaire;

// affichage de la solution P2P1

plot(p, wait = 1, fill = 1, value = 1, cmm = "champ scalaire de pression P1");
plot([ ux, uy ], wait = 1, fill = 1, value = 1,
     cmm = "champ vectoriel de vitesse P2");












